$(
  ---------------------------------------------------------------------------------------------------------------------
  LICENSE
  ---------------------------------------------------------------------------------------------------------------------
  Copyright 2020 @rbasniak (https://github.com/rbasniak)
  Permission is hereby granted, free of charge, to any person obtaining a copy of this software and associated
  documentation files (the "Software"), to deal in the Software without restriction, including without limitation
  the rights to use, copy, modify, merge, publish, distribute, sublicense, and/or sell copies of the Software, and
  to permit persons to whom the Software is furnished to do so, subject to the following conditions:

  The above copyright notice and this permission notice shall be included in all copies or substantial portions of
  the Software.

  THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO
  THE WARRANTIES OF MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
  AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT,
  TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
  SOFTWARE.

  ---------------------------------------------------------------------------------------------------------------------
  DOCUMENTATION
  ---------------------------------------------------------------------------------------------------------------------
  * VERSION
      v1.0.0
  * DESCRIPTION
      Function to help recursively locate all files with a given name in the specified folder.

  * INPUTS
      1) !path [STRING]: the path of the search directory
      2) !filename [STRING]: the filename to be searched

  * OUTPUT
      Array with the full path of all found files

  * USAGE
      !results = !!rbkFindFilesInFolder('c:\aveva\pmllib', 'myfile.pmlfnc')
$)

define function !!rbkFindFilesInFolder(!path is STRING, !filename is STRING) is ARRAY
  !directory = object FILE(!path)
  !results = ARRAY()

  -- Normalize the path to always end with \
  !normalizedPath = !path
  if not !path.matchwild('*\') then
    !normalizedPath = !path + '\'
  endif

  -- Do nothing when the directory does not exist
  if not !directory.exists() then
    return !results
  endif

  !directories = !directory.subDirs()
  !files = !directory.files()

  -- Seach the file in all files directly under the folder
  do !i from 1 to !files.size()
    !name = '$!normalizedPath$!filename'
    if !name.upcase() eq !files[!i].fullname().upcase() then
      !results.append('$!normalizedPath$!filename')
    endif
  enddo

  -- Recursivelly search all child directories
  do !i from 1 to !directories.size()
    !results.appendArray(!!rbkFindFilesInFolder(!directories[!i].fullname(), !filename))
  enddo

  return !results
endfunction